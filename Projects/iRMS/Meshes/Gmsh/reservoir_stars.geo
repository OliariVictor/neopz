// ---- iMRS reservoir geometry Gmsh scritp ----
// Creates a mesh with an inner structured-quad region and 
// an outer unstructured triangle region
//
// Created 10/01/2016 by Omar Duran
// Labmec, State University of Campinas
// --------------------------------------------

// Settings
ExpertMode = 1;
Mesh.Optimize = 1;
//Mesh.CharacteristicLengthExtendFromBoundary = 1;

Include "CadReservoir.geo";
Include "BoxReservoir.geo";
Include "BoxSideBurden.geo";
Include "BuildOmegas.geo";
Include "drill_well.geo";
Include "drill_well_box.geo";
Include "PhysicalEntities.geo";

well_index = 0;
well_p_lids = {};
well_i_lids = {};

well_p_bores = {};
well_p_regions = {};
well_p_v_regions = {};

well_i_bores = {};
well_i_regions = {};
well_i_v_regions = {};


geomechanicQ = 1;
dimension = 2;
nolinearQ = 0;
CADReservoirQ = 0;

xzQ = 0;
hexahedronsWQ = 1;
hexahedronsRQ = 1;
hexahedronsSBQ = 1;

If (nolinearQ == 1)
Mesh.ElementOrder = 2;
Mesh.SecondOrderLinear = 0;
EndIf

If (hexahedronsWQ == 1 || hexahedronsRQ == 1 || hexahedronsSBQ == 1)
Mesh.Algorithm3D = 6 ;
Else
Mesh.Algorithm3D = 1 ;
EndIf


// Gmsh allows variables; these will be used to set desired
// element sizes at various Points
cl1 = 1;
cl2 = 0.1;
cl3 = 10.0;
cl4 = 10.0;
cl5 = 750.0;

////////////////////////////////////////////////////////////////////////////
// reservoir region geometry
////////////////////////////////////////////////////////////////////////////

// reservoir box dimensions
x_length = 1010.2586585;
y_length = 50;
z_length = 50.0;

////////////////////////////////////////////////////////////////////////////
// side-burden region geometry
////////////////////////////////////////////////////////////////////////////

// side-burden box dimensions
sb_x_length = 2*3548.7804878;
sb_y_length = 2*1244.5121951;
sb_z_length = 2000.0;


////////////////////////////////////////////////////////////////////////////
// reservoir rock
////////////////////////////////////////////////////////////////////////////
If(CADReservoirQ == 1)
Call ReservoirCAD;
Else
Call ReservoirBox;
EndIf

////////////////////////////////////////////////////////////////////////////
// side-burden rock
////////////////////////////////////////////////////////////////////////////
If (geomechanicQ == 1)
Call SideBurdenBox;
EndIf


////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////
// Drilling wells 
////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////


////////////////////////////////////////////////////////////////////////////
// well geometry and settings
////////////////////////////////////////////////////////////////////////////

rw = 0.2;
wl = 50.0;

wbr = 10.0;
ela = 50.0;
rw_cell= 1.0;
wr_cell= 10.0;

If(dimension == 2)
rw = 0.2;
rw_cell= 1.0;
EndIf

// Orientation and length
alfa = 1.0*Pi/2.0;
beta = 0.0;

////////////////////////////////////////////////////////////////////////////
// well 1 
////////////////////////////////////////////////////////////////////////////

// well location
wcx = 0.0;
wcy = 0.0;
wcz = 0.0;

IsInjectorQ = 0;
Call DrillWell;


////////////////////////////////////////////////////////////////////////////
// well 2 
////////////////////////////////////////////////////////////////////////////

// well location
wcx = -(500-95.024329268);
wcy = 0.0;
wcz = 0.0;

IsInjectorQ = 1;
Call DrillWell;


////////////////////////////////////////////////////////////////////////////
// well 3 
////////////////////////////////////////////////////////////////////////////

// well location
wcx = (500-95.024329268);
wcy = 0.0;
wcz = 0.0;

IsInjectorQ = 1;
Call DrillWell;


////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////
// Computational domain, boundaries:: Tagging physical entities
////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////

Call DefineOmegas;
Call DrawBoundaries;
